apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

// Dokka is optional but recommended.
// Sonatype will not accept a published artifact without at least an empty Javadoc JAR.
task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = releaseConfig.group
                version = releaseConfig.version

                artifactId = 'IndicatorSeekBar'

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'IndicatorSeekBar'
                    description = 'Modification of https://github.com/warkiz/IndicatorSeekBar which allows adding custom tick positions'
                    url = 'https://github.com/infinum/IndicatorSeekBar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'BraneRi'
                            name = 'Branimir'
                            email = 'branimir.akmadza@infinum.hr'
                        }
                    }
                    scm {
                        connection = 'https://github.com/infinum/IndicatorSeekBar.git'
                        developerConnection = 'https://github.com/infinum/IndicatorSeekBar.git'
                        url = 'https://github.com/infinum/IndicatorSeekBar'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}